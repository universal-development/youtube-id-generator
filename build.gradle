buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.2"
    id 'com.palantir.git-version' version '0.12.3'
}

subprojects {

    group = 'com.unidev.idgenerator'
    version gitVersion()

    def details = versionDetails()
    printf details.toString()

    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        mavenCentral()
    }


    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc.failOnError = false
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    // bintray upload -Pbintray + master branch
    if (project.hasProperty('bintray') && 'master' == details.branchName) {

        bintray {
            user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
            configurations = ['archives']

            pkg {
                repo = 'unidev-libs'
                name = 'youtube-id-generator'
                userOrg = 'universal-development'
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/universal-development/youtube-id-generator'
                publish = true
                version {
                    name = "${project.version}"
                    desc = "${project.version}"
                    released = new Date()
                }
            }
        }

    }

    dependencies {
        testCompile("org.assertj:assertj-core:3.9.1")

        testImplementation(
                'org.junit.jupiter:junit-jupiter-api:5.1.0'
        )
        testRuntimeOnly(
                'org.junit.jupiter:junit-jupiter-engine:5.1.0'
        )
    }

}
